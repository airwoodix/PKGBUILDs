# Maintainer: airwoodix <airwoodix at posteo.me>
_name=flask-autodoc
_owner=airwoodix
pkgname=python-flask-autodoc-git
pkgver=0.1.2.r14.g735c67d
pkgrel=1
pkgdesc="Automatic documentation for Flask"
arch=('any')
url="https://github.com/$_owner/$_name"
license=('MIT')
depends=('python' 'python-flask>=1.0' 'python-setuptools')
provides=('python-flask-autodoc')
conflicts=('python-flask-autodoc')
options=(!emptydirs)
source=("git+https://github.com/$_owner/$_name.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_name"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$_name"
    python setup.py build
}

package() {
  cd "$srcdir/$_name"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
